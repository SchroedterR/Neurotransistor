** Neurotransistor with memristor-based memcapacitor
.SUBCKT neurotransistor IN D S G SV
** Parameter
.params Icc=1e-3 xmax=1 xmin=0
+ion=1e-6 ioff=100e-9 isb=1.3e-7 etas=100 etar=100
+Vs=0.3 Vr=-0.6 aon=4 aoff=4 Rp=10e6 Rs=100
+Cp=10e-12 Cgs=100e-12 Rdd=10
** Circuit
* Series resistance
Ri IN B {Rs}
* Current source for memristor IV response
Gm G B value={limit(Im(V(SV,0),V(G,B)),-Icc,Icc)}
* Parallel resistance and capacitance
Rm IN G {Rp}
Cm IN G {Cp}
* MOSFET circuit
Mn Nd G S IHM
* Gate-source capacitance
Cg G S {Cgs}
* Drain resistance
Rd Nd D {Rdd}
* State dynamics dx/dt = F(V,x)
Cx SV 0 {1} ic = {x0}
Gx 0 SV value = {F(V(SV,0),V(G,IN))}
** Functions
*Linear state dependency
.func K(x,on,off)=off+(on-off)*limit(x,xmin,xmax)
*Diode-like current function
.func Im(x,vm)= K(x,ion,ioff)*sinh(K(x,aon,aoff)*vm)
*Set time constant
.func TS(v)=exp(-etas*(v-if(I(Gm)>isb,0,Vs)))
*Reset time constant
.func TR(v)=exp( etar*(v-Vr))
*State equation for state in range of [0,1]
.func F(x,v)= if(v>=0,(xmax-x)/TS(v),(xmin-x)/TR(v))
** NMOS transistor model
.model IHM NMOS(LEVEL=3 L=26u W=94u Vto=0.33 Tox=22n
+Uo=798 PHI=66)
.ENDS neurotransistor
