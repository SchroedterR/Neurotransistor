** Memristor with abrupt switching
* created by Richard Schroedter 2022
.SUBCKT MemAS + - SV
** parameter
.params Icc=1e-3 xmax=1 xmin=0
+ion=1e-6 ioff=100e-9 isb=1.3e-7 etas=100 etar=100
+Vs=0.3 Vr=-0.6 aon=4 aoff=4 Cp=10p Rp=10e6 Rs=100

* Series resistance
Ri + D {Rs}
* Current source for memristor IV response
Gm D - value={limit(Im(V(SV,0),V(D,-)),-Icc,Icc)}
* Parallel resistance
Rm + - {Rp}

* State dynamics dx/dt = F(V,x)
Cx SV 0 {1} ic = {x0}
Gx 0 SV value = {F(V(SV,0),V(+,-))}

** Functions
*linear state dependency
.func K(x,on,off)=off+(on-off)*limit(x,xmin,xmax)
*diode-like current function
.func Im(x,vm)= K(x,ion,ioff)*sinh(K(x,aon,aoff)*vm)
*set time constant
.func TS(v)=exp(-etas*(v-if(I(Gm)>isb,0,Vs)))
*reset time constant
.func TR(v)=exp( etar*(v-Vr))
*State equation for state in range of [0,1]
.func F(x,v)= if(v>=0,(xmax-x)/TS(v),(xmin-x)/TR(v))

.ENDS MemAS
